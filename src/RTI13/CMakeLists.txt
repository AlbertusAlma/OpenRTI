# The directory containing the public api for RTI13
SET(RTI13_HEADER_PATH ${CMAKE_SOURCE_DIR}/include/RTI13)

# All the -I arguments
INCLUDE_DIRECTORIES(${RTI13_HEADER_PATH})
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR})
# The OpenRTI implementation files and may be build generated sources
INCLUDE_DIRECTORIES(${CMAKE_BINARY_DIR}/src/OpenRTI)
INCLUDE_DIRECTORIES(${CMAKE_SOURCE_DIR}/src/OpenRTI)

# The Headers for libRTI-NG and libFedTime
SET(RTI13_PUBLIC_HEADERS
        ${RTI13_HEADER_PATH}/baseTypes.hh
        ${RTI13_HEADER_PATH}/federateAmbServices.hh
        ${RTI13_HEADER_PATH}/fedtime.hh
        ${RTI13_HEADER_PATH}/NullFederateAmbassador.hh
        ${RTI13_HEADER_PATH}/RTIambServices.hh
        ${RTI13_HEADER_PATH}/RTI.hh
        ${RTI13_HEADER_PATH}/RTItypes.hh
)

SET(LIB_RTI_NG_SOURCES
	RTIambPrivateRefs.cpp
        AttributeHandleSetCallback.cpp
        AttributeHandleSetImplementation.cpp
        AttributeHandleValuePairSetCallback.cpp
        AttributeHandleValuePairSetImplementation.cpp
        Exception.cpp
        FederateHandleSetImplementation.cpp
        ParameterHandleValuePairSetCallback.cpp
        ParameterHandleValuePairSetImplementation.cpp
        RTI13LogicalTimeFactory.cpp
)

SET(LIB_FEDTIME_SOURCES RTIfedTime.cpp)

# RTI-NG, the circular reference is tricky on win32
ADD_LIBRARY(RTI-NG SHARED ${LIB_RTI_NG_SOURCES})
SET_PROPERTY(TARGET RTI-NG APPEND PROPERTY COMPILE_DEFINITIONS RTI_EXPORTS)
SET_PROPERTY(TARGET RTI-NG PROPERTY VERSION 1.3.0)
SET_PROPERTY(TARGET RTI-NG PROPERTY SOVERSION 1)
SET_PROPERTY(TARGET RTI-NG PROPERTY LINK_INTERFACE_LIBRARIES FedTime)
IF(MSVC)
  ADD_LIBRARY(FedTimeStub STATIC ${LIB_FEDTIME_SOURCES})
  SET_PROPERTY(TARGET FedTimeStub APPEND PROPERTY COMPILE_DEFINITIONS FedTime_EXPORTS)
  SET_PROPERTY(TARGET FedTimeStub APPEND PROPERTY STATIC_LIBRARY_FLAGS "/DEF /NAME:FedTime.dll")
  TARGET_LINK_LIBRARIES(RTI-NG FedTimeStub OpenRTI)
ELSE()
  TARGET_LINK_LIBRARIES(RTI-NG OpenRTI)
ENDIF()
IF(MACOS)
  SET_PROPERTY(TARGET RTI-NG PROPERTY LINK_FLAGS "-Wl,-undefined,dynamic_lookup")
ENDIF()

# libFedTime
ADD_LIBRARY(FedTime SHARED ${LIB_FEDTIME_SOURCES})
SET_PROPERTY(TARGET FedTime APPEND PROPERTY COMPILE_DEFINITIONS FedTime_EXPORTS)
SET_PROPERTY(TARGET FedTime PROPERTY VERSION 1.3.0)
SET_PROPERTY(TARGET FedTime PROPERTY SOVERSION 1)
SET_PROPERTY(TARGET FedTime PROPERTY LINK_INTERFACE_LIBRARIES "")
TARGET_LINK_LIBRARIES(FedTime RTI-NG OpenRTI)


INSTALL(TARGETS FedTime RTI-NG
  RUNTIME DESTINATION bin
  LIBRARY DESTINATION lib
  ARCHIVE DESTINATION lib)
# INSTALL(FILES ${RTI13_PUBLIC_HEADERS}
#   DESTINATION include/RTI13)
INSTALL(FILES ${RTI13_PUBLIC_HEADERS}
  DESTINATION include)
